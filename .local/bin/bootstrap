#!/usr/bin/env fish

# Try to find my own configuration script
string match --quiet "$HOME*" (command --search config) # in PATH, that's good
or alias config (status dirname)/config # in same directory as current script (hopefully)


echo "[!] Generate and set locale"
# https://wiki.archlinux.org/title/Locale
config --file=/etc/locale.gen
and locale-gen
and config --file=/etc/locale.conf

echo "[!] Set time zone and enable NTP service"
# https://wiki.archlinux.org/title/System_time#Time_zone
test -L /etc/localtime # should be symbol link to /usr/share/zoneinfo/...
or timedatectl set-timezone Asia/Shanghai
# https://man.archlinux.org/man/systemd-timesyncd.service.8
systemctl is-active --quiet systemd-timesyncd.service
or timedatectl set-ntp true

echo "[*] Configure pacman"
config --file=/etc/pacman.conf
# https://github.com/archlinuxcn/repo#usage
contains archlinuxcn (pacman-conf --repo-list)
and pacman --sync --refresh # -Sy
# https://www.archlinuxcn.org/archlinuxcn-keyring-manually-trust-farseerfc-key/
and pacman-key --lsign-key "farseerfc@archlinux.org"
and pacman --sync archlinuxcn-keyring

echo "[!] Install packages"
set pkgs (cat (fallback $XDG_CONFIG_HOME ~/.config)/package-list/fish)
set --append pkgs helix rsync mosh base-devel
contains archlinuxcn (pacman-conf --repo-list)
and set --append pkgs fastfetch paru
pacman --sync --refresh --sysupgrade $pkgs # -Syu

echo "[*] Cleanup bash dotfiles"
for file in ~/.bash*
    set --local new_name (basename $file).(string sub --length 7 (sha256sum $file))
    mv --interactive --verbose $file (fallback $XDG_CACHE_HOME ~/.cache)/$new_name
end

echo "[!] Disable SSHD password authentication if user public key file exists"
# https://wiki.archlinux.org/title/OpenSSH#Force_public_key_authentication
test -s ~/.ssh/authorized_keys # exists and size greater than zero
and config --file=/etc/ssh/sshd_config
and systemctl restart sshd

echo "[!] Enable BBR"
# https://wiki.archlinux.org/title/Sysctl#Enable_BBR
and config --file=/etc/sysctl.d/51-bbr.conf
and sysctl --load=/etc/sysctl.d/51-bbr.conf
