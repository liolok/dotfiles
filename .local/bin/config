#!/usr/bin/env fish
# Mess around with configuration files

set status_err_arg 1
set status_err_file 2
set status_no_change 3

argparse --min-args=1 "file=" -- $argv; or exit $status_err_arg

if not set --query _flag_file or test -z "$_flag_file"
    echo 'Error: missing option --file or its argument'
    exit $status_err_arg
end

set file $_flag_file

if test -d $file
    echo "Error: $file is not a file but a directory"
    exit $status_err_file
end

test -L $file; and set file (realpath $file)

if not test -w $file
    echo "Error: $file is not marked as writable"
    exit $status_err_file
end

if test -s "$file" # file already exists and has content
    for arg in $argv
        if grep --line-number --word-regexp "^$arg" $file
            echo "Info: configuration $arg already exists, won't be changed"
            set --erase argv[(contains --index $arg $argv)] # delete item
        end
    end
    if test -z "$argv"
        echo "Info: Nothing to configure, no changes to $file at all"
        exit $status_no_change
    end

    set suffix (string sub --length 7 (sha256sum $file))
    set backup $file.$suffix
    if not cp --archive --verbose $file $backup
        echo "Warn: failed to backup $backup"
        set --erase backup
    end
end

for arg in $argv
    set --local key (string split ' ' $arg --fields 1)
    if grep --quiet --word-regexp "^#$key" $file
        sed --in-place "s/^#$key.*\$/$arg/" $file; or exit
    else
        echo $arg | tee --append $file; or exit
    end
end

test -f "$backup"; and diff --color $backup $file
