#!/bin/fish

# User function backup
functions --query fallback || alias fallback \
"begin; for v in $argv; if test -n $v; echo $v; break; end; end; end; end"

# Launch app from systemd-nspawn container
set container_dir /var/lib/machines
set container_app (basename (status filename)) # get from current file base name
set app $container_app
set home /home/$app # dedicated home for app in container
set home_conf $home/.config
set home_data $home/.local/share

# Commandline options for systemd-nspawn
set options --as-pid2 --directory=$container_dir/$app --user=$app --chdir=$home
alias add_options 'set --append options'

# Tray
set host_bus (fallback (string trim --left --chars=unix:path= $DBUS_SESSION_BUS_ADDRESS) \
    /run/user/(id --user)/bus)
set container_bus /run/user/host/bus
add_options --bind-ro=$host_bus:$container_bus \
            --setenv=DBUS_SESSION_BUS_ADDRESS=unix:path=$container_bus

# Sound
set host_pulse (fallback (string trim --left --chars=unix: $PULSE_SERVER) \
    /run/user/(id --user)/pulse/native)
set container_pulse /run/user/host/pulse
add_options --bind-ro=$host_pulse:$container_pulse \
            --setenv=PULSE_SERVER=unix:$container_pulse

# Icons
set container_icons $home_data/icons
add_options --bind-ro=/usr/share/icons:$container_icons \
            --setenv=XCURSOR_PATH=$container_icons

# Fonts
set host_fontconf (fallback $XDG_CONFIG_HOME ~/.config)/fontconfig
set container_fonts $home_data/fonts
add_options --bind-ro=/usr/share/fonts:$container_fonts \
            --bind-ro=$host_fontconf:$home_conf/fontconfig

# Screenshots
set host_screenshots (fallback (xdg-user-dir PICTURES) ~/Pictures)/Screenshots/$app
mkdir --parents $host_screenshots
add_options --bind=$host_screenshots:$home/screenshots

# Authorization for X, seems not necessary under Gnome.
# set xauth_file /tmp/xauth-$container_name
# xauth nextract - "$DISPLAY" | sed -e 's/^..../ffff/' | xauth -f "$xauth_file" nmerge -
# add_options --bind=$xauth_file --setenv=XAUTHORITY=$xauth_file

# Display
add_options --bind-ro=/tmp/.X11-unix/ --setenv=DISPLAY=$DISPLAY

# Devices
add_options --bind=/dev/dri/ --property=DeviceAllow=char-drm\ rwm \
            --bind=/dev/input/ --property=DeviceAllow=char-input\ rwm

# Scale
if test $hostname = acer-sf
    add_options --setenv=GDK_SCALE=2 --setenv=GDK_DPI_SCALE=0.5
end

echo "List of cmdline options applied to systemd-nspawn:"
printf "%s\n" $options | sort

# Run container directly into target application
sudo systemd-nspawn $options $app $argv
