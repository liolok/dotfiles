#!/bin/fish

# User function backup
functions --query fallback; or function fallback --argument-names "list" \
    --description "Fallback to first non-zero string in given list"
    begin; for v in $list; if test -n $v; echo $v; break; end; end; end; end

# Launch app from systemd-nspawn container
set file_name (basename (status filename)) # get current file base name
set app $file_name
set home /home/$app # dedicated home for app in container
set host_conf (fallback $XDG_CONFIG_HOME ~/.config)
set conf $home/.config
set host_data /usr/share
set data $home/.local/share

# Commandline options for systemd-nspawn
set options --as-pid2 --directory=/var/lib/machines/$app --user=$app --chdir=$home
alias add_options 'set --append options'

# Tray
set host_dbus (fallback \
    (string trim --left --chars=unix:path= $DBUS_SESSION_BUS_ADDRESS) \
    /run/user/(id --user)/bus)
set dbus /run/user/host/bus
add_options --bind-ro=$host_dbus:$dbus \
            --setenv=DBUS_SESSION_BUS_ADDRESS=unix:path=$dbus

# Sound
set host_pulse (fallback \
    (string trim --left --chars=unix: $PULSE_SERVER) \
    /run/user/(id --user)/pulse/native)
set pulse /run/user/host/pulse
add_options --bind-ro=$host_pulse:$pulse \
            --setenv=PULSE_SERVER=unix:$pulse

# Icons
set icons $data/icons
add_options --bind-ro=$host_data/icons:$icons \
    --setenv=XCURSOR_PATH=$icons

# Fonts
add_options --bind-ro=$host_data/fonts:$data/fonts \
            --bind-ro=$host_conf/fontconfig:$conf/fontconfig

# Screenshots
set host_screenshots (fallback (xdg-user-dir PICTURES) ~/Pictures)/Screenshots/$app
mkdir --parents $host_screenshots
add_options --bind=$host_screenshots:$home/screenshots

# Authorization for X, seems not necessary under Gnome.
# (https://wiki.archlinux.org/index.php/Systemd-nspawn#Avoiding_xhost)
# set xauth_file /tmp/xauth-$app
# xauth nextract - "$DISPLAY" | sed -e 's/^..../ffff/' | xauth -f "$xauth_file" nmerge -
# add_options --bind=$xauth_file --setenv=XAUTHORITY=$xauth_file

# Display
add_options --bind-ro=/tmp/.X11-unix/ --setenv=DISPLAY=$DISPLAY

# Devices
add_options --bind=/dev/dri/ --property=DeviceAllow=char-drm\ rwm \
            --bind=/dev/input/ --property=DeviceAllow=char-input\ rwm

# Scale
if test $hostname = acer-sf
    add_options --setenv=GDK_SCALE=2 --setenv=GDK_DPI_SCALE=0.5
end

echo "List of cmdline options applied to systemd-nspawn:"
printf "%s\n" $options | sort

# Run container directly into target app, passthrough all arguments.
sudo systemd-nspawn $options $app $argv
